           ORIG 3999
           JMP  BEGIN
*
*           M I X   A S S E M B L Y   P R O G R A M
*
*               D. KNUTH
*               AUGUST, 1964
*               CORRECTION MARCH 25, 1965
*
BYTESIZE   EQU  1(4-4)
READER     EQU  16
PRINTER    EQU  18
TAPE       EQU  1
TBUF       EQU  0
PBUF1      EQU  TBUF+101
PBUF2      EQU  PBUF1+26
RBUF1      EQU  PBUF2+25
RBUF2      EQU  RBUF1+17
TABLE1     EQU  850
SIZE       EQU  1050
EQUIV      EQU  TABLE1+SIZE
TABLE2     EQU  EQUIV+SIZE
*
*          CONTENTS OF BUFFER AREAS
*
           ORIG 0
LOADER     ENT5 140,6
           ENT3 100
1H         ENN3 0,3
2H         IN   0,5(TAPE)
           INC5 0,3
           ENT1 99
           ENT2 99,5
           LDA  99,5
           DEC2 1
           ADD  0,2
           DEC1 1
           J1P  *-3,6
           JAZ  2F,6
           HLT  1
           IOC  2(TAPE)
           IN   0,5(TAPE)
           DEC5 0,3
           JMP  2B,6
2H         LD4  0,2(5-5)
           ENT1 98,5
           J4Z  5F,6
           LD1  0,2(3-4)
           ST4  *+1,6(4-4)
           MOVE 1,2(*)
           INC2 1,4
           JMP  2B,6
3H         DEC2 1
           LD4  0,2(0-2)
           STA  0,2(0-2)
           ENT2 0,4
           J4NN *-3,6
4H         DEC1 1
5H         LD2  0,1(4-5)
           LDA  0,1(0-2)
           J2P  3B,6
           JAZ  1B,6
           IOC  0(TAPE)
           LD1  0,1(2-3)
           JOV  0,1
           JMP  0,1
           ORIG PBUF1-1
           CON  0
           ALF  .....
           ALF   MIX
           ALF  ASSEM
           ALF  BLY P
           ALF  ROGRA
           ALF  M ...
           ALF  .....
           ALF  .....
           ALF  .....
           ALF  .....
           ALF  .....
           ALF  .....
           ALF  .....
           ALF  .....
           ALF  .....
           ALF  .....
           ALF  .....
           ALF  .....
           ALF  .....
           ALF  .....
           ALF  .....
           ALF  .....
           ALF  .....
           ALF  .....
           CON  PBUF2
           CON  0
           ORIG PBUF2+24
           CON  PBUF1
           ORIG RBUF1+16
           CON  RBUF2
           ORIG RBUF2+16
           CON  RBUF1
           CON  0
BOUND      CON  0
           CON  0
           CON  0
           CON  0
           CON  0
           CON  1
FTABLE     ORIG *+10
BTABLE     ORIG *+10
*
*              OPERATION CODES
*
0H         EQU  *
           ORIG TABLE1+0977
           ALF   NOP
           ORIG EQUIV+0977
           CON  00,0(4-4)
           ORIG TABLE1+0590
           ALF   ADD
           ORIG EQUIV+0590
           CON  01,5(4-4)
           ORIG TABLE1+0548
           ALF   SUB
           ORIG EQUIV+0548
           CON  02,5(4-4)
           ORIG TABLE1+0469
           ALF   MUL
           ORIG EQUIV+0469
           CON  03,5(4-4)
           ORIG TABLE1+0104
           ALF   DIV
           ORIG EQUIV+0104
           CON  04,5(4-4)
           ORIG TABLE1+0251
           ALF   NUM
           ORIG EQUIV+0251
           CON  05,0(4-4)
           ORIG TABLE1+0243
           ALF   CHAR
           ORIG EQUIV+0243
           CON  05,1(4-4)
           ORIG TABLE1+0849
           ALF   HLT
           ORIG EQUIV+0849
           CON  05,2(4-4)
           ORIG TABLE1+1028
           ALF   SLA
           ORIG EQUIV+1028
           CON  06,0(4-4)
           ORIG TABLE1+0020
           ALF   SRA
           ORIG EQUIV+0020
           CON  06,1(4-4)
           ORIG TABLE1+0700
           ALF   SLAX
           ORIG EQUIV+0700
           CON  06,2(4-4)
           ORIG TABLE1+0741
           ALF   SRAX
           ORIG EQUIV+0741
           CON  06,3(4-4)
           ORIG TABLE1+0616
           ALF   SLC
           ORIG EQUIV+0616
           CON  06,4(4-4)
           ORIG TABLE1+0657
           ALF   SRC
           ORIG EQUIV+0657
           CON  06,5(4-4)
           ORIG TABLE1+0482
           ALF   MOVE
           ORIG EQUIV+0482
           CON  07,1(4-4)
           ORIG TABLE1+0019
           ALF   LDA
           ORIG EQUIV+0019
           CON  08,5(4-4)
           ORIG TABLE1+0128
           ALF   LD1
           ORIG EQUIV+0128
           CON  09,5(4-4)
           ORIG TABLE1+0971
           ALF   LD2
           ORIG EQUIV+0971
           CON  10,5(4-4)
           ORIG TABLE1+0765
           ALF   LD3
           ORIG EQUIV+0765
           CON  11,5(4-4)
           ORIG TABLE1+0558
           ALF   LD4
           ORIG EQUIV+0558
           CON  12,5(4-4)
           ORIG TABLE1+0352
           ALF   LD5
           ORIG EQUIV+0352
           CON  13,5(4-4)
           ORIG TABLE1+0145
           ALF   LD6
           ORIG EQUIV+0145
           CON  14,5(4-4)
           ORIG TABLE1+0953
           ALF   LDX
           ORIG EQUIV+0953
           CON  15,5(4-4)
           ORIG TABLE1+0304
           ALF   LDAN
           ORIG EQUIV+0304
           CON  16,5(4-4)
           ORIG TABLE1+0412
           ALF   LD1N
           ORIG EQUIV+0412
           CON  17,5(4-4)
           ORIG TABLE1+0205
           ALF   LD2N
           ORIG EQUIV+0205
           CON  18,5(4-4)
           ORIG TABLE1+1049
           ALF   LD3N
           ORIG EQUIV+1049
           CON  19,5(4-4)
           ORIG TABLE1+0842
           ALF   LD4N
           ORIG EQUIV+0842
           CON  20,5(4-4)
           ORIG TABLE1+0636
           ALF   LD5N
           ORIG EQUIV+0636
           CON  21,5(4-4)
           ORIG TABLE1+0429
           ALF   LD6N
           ORIG EQUIV+0429
           CON  22,5(4-4)
           ORIG TABLE1+0187
           ALF   LDXN
           ORIG EQUIV+0187
           CON  23,5(4-4)
           ORIG TABLE1+0397
           ALF   STA
           ORIG EQUIV+0397
           CON  24,5(4-4)
           ORIG TABLE1+0504
           ALF   ST1
           ORIG EQUIV+0504
           CON  25,5(4-4)
           ORIG TABLE1+0298
           ALF   ST2
           ORIG EQUIV+0298
           CON  26,5(4-4)
           ORIG TABLE1+0092
           ALF   ST3
           ORIG EQUIV+0092
           CON  27,5(4-4)
           ORIG TABLE1+0935
           ALF   ST4
           ORIG EQUIV+0935
           CON  28,5(4-4)
           ORIG TABLE1+0729
           ALF   ST5
           ORIG EQUIV+0729
           CON  29,5(4-4)
           ORIG TABLE1+0522
           ALF   ST6
           ORIG EQUIV+0522
           CON  30,5(4-4)
           ORIG TABLE1+0280
           ALF   STX
           ORIG EQUIV+0280
           CON  31,5(4-4)
           ORIG TABLE1+0433
           ALF   STJ
           ORIG EQUIV+0433
           CON  32,2(4-4)
           ORIG TABLE1+0917
           ALF   STZ
           ORIG EQUIV+0917
           CON  33,5(4-4)
           ORIG TABLE1+0458
           ALF   JBUS
           ORIG EQUIV+0458
           CON  34,0(4-4)
           ORIG TABLE1+0786
           ALF   IOC
           ORIG EQUIV+0786
           CON  35,0(4-4)
           ORIG TABLE1+1048
           ALF   IN
           ORIG EQUIV+1048
           CON  36,0(4-4)
           ORIG TABLE1+0481
           ALF   OUT
           ORIG EQUIV+0481
           CON  37,0(4-4)
           ORIG TABLE1+0867
           ALF   JRED
           ORIG EQUIV+0867
           CON  38,0(4-4)
           ORIG TABLE1+0310
           ALF   JMP
           ORIG EQUIV+0310
           CON  39,0(4-4)
           ORIG TABLE1+0203
           ALF   JSJ
           ORIG EQUIV+0203
           CON  39,1(4-4)
           ORIG TABLE1+0422
           ALF   JOV
           ORIG EQUIV+0422
           CON  39,2(4-4)
           ORIG TABLE1+0296
           ALF   JNOV
           ORIG EQUIV+0296
           CON  39,3(4-4)
           ORIG TABLE1+0312
           ALF   JL
           ORIG EQUIV+0312
           CON  39,4(4-4)
           ORIG TABLE1+0607
           ALF   JE
           ORIG EQUIV+0607
           CON  39,5(4-4)
           ORIG TABLE1+0271
           ALF   JG
           ORIG EQUIV+0271
           CON  39,6(4-4)
           ORIG TABLE1+0288
           ALF   JGE
           ORIG EQUIV+0288
           CON  39,7(4-4)
           ORIG TABLE1+1044
           ALF   JNE
           ORIG EQUIV+1044
           CON  39,8(4-4)
           ORIG TABLE1+0330
           ALF   JLE
           ORIG EQUIV+0330
           CON  39,9(4-4)
           ORIG TABLE1+0283
           ALF   JAN
           ORIG EQUIV+0283
           CON  40,0(4-4)
           ORIG TABLE1+0489
           ALF   J1N
           ORIG EQUIV+0489
           CON  41,0(4-4)
           ORIG TABLE1+0846
           ALF   J2N
           ORIG EQUIV+0846
           CON  42,0(4-4)
           ORIG TABLE1+0153
           ALF   J3N
           ORIG EQUIV+0153
           CON  43,0(4-4)
           ORIG TABLE1+0510
           ALF   J4N
           ORIG EQUIV+0510
           CON  44,0(4-4)
           ORIG TABLE1+0866
           ALF   J5N
           ORIG EQUIV+0866
           CON  45,0(4-4)
           ORIG TABLE1+0174
           ALF   J6N
           ORIG EQUIV+0174
           CON  46,0(4-4)
           ORIG TABLE1+0112
           ALF   JXN
           ORIG EQUIV+0112
           CON  47,0(4-4)
           ORIG TABLE1+0543
           ALF   JAZ
           ORIG EQUIV+0543
           CON  40,1(4-4)
           ORIG TABLE1+0749
           ALF   J1Z
           ORIG EQUIV+0749
           CON  41,1(4-4)
           ORIG TABLE1+0056
           ALF   J2Z
           ORIG EQUIV+0056
           CON  42,1(4-4)
           ORIG TABLE1+0413
           ALF   J3Z
           ORIG EQUIV+0413
           CON  43,1(4-4)
           ORIG TABLE1+0770
           ALF   J4Z
           ORIG EQUIV+0770
           CON  44,1(4-4)
           ORIG TABLE1+0077
           ALF   J5Z
           ORIG EQUIV+0077
           CON  45,1(4-4)
           ORIG TABLE1+0434
           ALF   J6Z
           ORIG EQUIV+0434
           CON  46,1(4-4)
           ORIG TABLE1+0372
           ALF   JXZ
           ORIG EQUIV+0372
           CON  47,1(4-4)
           ORIG TABLE1+0920
           ALF   JAP
           ORIG EQUIV+0920
           CON  40,2(4-4)
           ORIG TABLE1+0076
           ALF   J1P
           ORIG EQUIV+0076
           CON  41,2(4-4)
           ORIG TABLE1+0432
           ALF   J2P
           ORIG EQUIV+0432
           CON  42,2(4-4)
           ORIG TABLE1+0790
           ALF   J3P
           ORIG EQUIV+0790
           CON  43,2(4-4)
           ORIG TABLE1+0097
           ALF   J4P
           ORIG EQUIV+0097
           CON  44,2(4-4)
           ORIG TABLE1+0454
           ALF   J5P
           ORIG EQUIV+0454
           CON  45,2(4-4)
           ORIG TABLE1+0811
           ALF   J6P
           ORIG EQUIV+0811
           CON  46,2(4-4)
           ORIG TABLE1+0748
           ALF   JXP
           ORIG EQUIV+0748
           CON  47,2(4-4)
           ORIG TABLE1+0567
           ALF   JANN
           ORIG EQUIV+0567
           CON  40,3(4-4)
           ORIG TABLE1+0773
           ALF   J1NN
           ORIG EQUIV+0773
           CON  41,3(4-4)
           ORIG TABLE1+0080
           ALF   J2NN
           ORIG EQUIV+0080
           CON  42,3(4-4)
           ORIG TABLE1+0437
           ALF   J3NN
           ORIG EQUIV+0437
           CON  43,3(4-4)
           ORIG TABLE1+0794
           ALF   J4NN
           ORIG EQUIV+0794
           CON  44,3(4-4)
           ORIG TABLE1+0101
           ALF   J5NN
           ORIG EQUIV+0101
           CON  45,3(4-4)
           ORIG TABLE1+0457
           ALF   J6NN
           ORIG EQUIV+0457
           CON  46,3(4-4)
           ORIG TABLE1+0396
           ALF   JXNN
           ORIG EQUIV+0396
           CON  47,3(4-4)
           ORIG TABLE1+0202
           ALF   JANZ
           ORIG EQUIV+0202
           CON  40,4(4-4)
           ORIG TABLE1+0409
           ALF   J1NZ
           ORIG EQUIV+0409
           CON  41,4(4-4)
           ORIG TABLE1+0764
           ALF   J2NZ
           ORIG EQUIV+0764
           CON  42,4(4-4)
           ORIG TABLE1+0072
           ALF   J3NZ
           ORIG EQUIV+0072
           CON  43,4(4-4)
           ORIG TABLE1+0428
           ALF   J4NZ
           ORIG EQUIV+0428
           CON  44,4(4-4)
           ORIG TABLE1+0785
           ALF   J5NZ
           ORIG EQUIV+0785
           CON  45,4(4-4)
           ORIG TABLE1+0093
           ALF   J6NZ
           ORIG EQUIV+0093
           CON  46,4(4-4)
           ORIG TABLE1+0031
           ALF   JXNZ
           ORIG EQUIV+0031
           CON  47,4(4-4)
           ORIG TABLE1+0815
           ALF   JANP
           ORIG EQUIV+0815
           CON  40,5(4-4)
           ORIG TABLE1+1021
           ALF   J1NP
           ORIG EQUIV+1021
           CON  41,5(4-4)
           ORIG TABLE1+0328
           ALF   J2NP
           ORIG EQUIV+0328
           CON  42,5(4-4)
           ORIG TABLE1+0685
           ALF   J3NP
           ORIG EQUIV+0685
           CON  43,5(4-4)
           ORIG TABLE1+1042
           ALF   J4NP
           ORIG EQUIV+1042
           CON  44,5(4-4)
           ORIG TABLE1+0349
           ALF   J5NP
           ORIG EQUIV+0349
           CON  45,5(4-4)
           ORIG TABLE1+0706
           ALF   J6NP
           ORIG EQUIV+0706
           CON  46,5(4-4)
           ORIG TABLE1+0644
           ALF   JXNP
           ORIG EQUIV+0644
           CON  47,5(4-4)
           ORIG TABLE1+0028
           ALF   INCA
           ORIG EQUIV+0028
           CON  48,0(4-4)
           ORIG TABLE1+0717
           ALF   DECA
           ORIG EQUIV+0717
           CON  48,1(4-4)
           ORIG TABLE1+0146
           ALF   ENTA
           ORIG EQUIV+0146
           CON  48,2(4-4)
           ORIG TABLE1+0747
           ALF   ENNA
           ORIG EQUIV+0747
           CON  48,3(4-4)
           ORIG TABLE1+0597
           ALF   INC1
           ORIG EQUIV+0597
           CON  49,0(4-4)
           ORIG TABLE1+0235
           ALF   DEC1
           ORIG EQUIV+0235
           CON  49,1(4-4)
           ORIG TABLE1+0714
           ALF   ENT1
           ORIG EQUIV+0714
           CON  49,2(4-4)
           ORIG TABLE1+0266
           ALF   ENN1
           ORIG EQUIV+0266
           CON  49,3(4-4)
           ORIG TABLE1+0195
           ALF   INC2
           ORIG EQUIV+0195
           CON  50,0(4-4)
           ORIG TABLE1+0884
           ALF   DEC2
           ORIG EQUIV+0884
           CON  50,1(4-4)
           ORIG TABLE1+0313
           ALF   ENT2
           ORIG EQUIV+0313
           CON  50,2(4-4)
           ORIG TABLE1+0914
           ALF   ENN2
           ORIG EQUIV+0914
           CON  50,3(4-4)
           ORIG TABLE1+0844
           ALF   INC3
           ORIG EQUIV+0844
           CON  51,0(4-4)
           ORIG TABLE1+0483
           ALF   DEC3
           ORIG EQUIV+0483
           CON  51,1(4-4)
           ORIG TABLE1+0962
           ALF   ENT3
           ORIG EQUIV+0962
           CON  51,2(4-4)
           ORIG TABLE1+0513
           ALF   ENN3
           ORIG EQUIV+0513
           CON  51,3(4-4)
           ORIG TABLE1+0443
           ALF   INC4
           ORIG EQUIV+0443
           CON  52,0(4-4)
           ORIG TABLE1+0082
           ALF   DEC4
           ORIG EQUIV+0082
           CON  52,1(4-4)
           ORIG TABLE1+0561
           ALF   ENT4
           ORIG EQUIV+0561
           CON  52,2(4-4)
           ORIG TABLE1+0111
           ALF   ENN4
           ORIG EQUIV+0111
           CON  52,3(4-4)
           ORIG TABLE1+0042
           ALF   INC5
           ORIG EQUIV+0042
           CON  53,0(4-4)
           ORIG TABLE1+0731
           ALF   DEC5
           ORIG EQUIV+0731
           CON  53,1(4-4)
           ORIG TABLE1+0160
           ALF   ENT5
           ORIG EQUIV+0160
           CON  53,2(4-4)
           ORIG TABLE1+0761
           ALF   ENN5
           ORIG EQUIV+0761
           CON  53,3(4-4)
           ORIG TABLE1+0691
           ALF   INC6
           ORIG EQUIV+0691
           CON  54,0(4-4)
           ORIG TABLE1+0329
           ALF   DEC6
           ORIG EQUIV+0329
           CON  54,1(4-4)
           ORIG TABLE1+0809
           ALF   ENT6
           ORIG EQUIV+0809
           CON  54,2(4-4)
           ORIG TABLE1+0360
           ALF   ENN6
           ORIG EQUIV+0360
           CON  54,3(4-4)
           ORIG TABLE1+0100
           ALF   INCX
           ORIG EQUIV+0100
           CON  55,0(4-4)
           ORIG TABLE1+0789
           ALF   DECX
           ORIG EQUIV+0789
           CON  55,1(4-4)
           ORIG TABLE1+0218
           ALF   ENTX
           ORIG EQUIV+0218
           CON  55,2(4-4)
           ORIG TABLE1+0820
           ALF   ENNX
           ORIG EQUIV+0820
           CON  55,3(4-4)
           ORIG TABLE1+0001
           ALF   CMPA
           ORIG EQUIV+0001
           CON  56,5(4-4)
           ORIG TABLE1+0569
           ALF   CMP1
           ORIG EQUIV+0569
           CON  57,5(4-4)
           ORIG TABLE1+0168
           ALF   CMP2
           ORIG EQUIV+0168
           CON  58,5(4-4)
           ORIG TABLE1+0817
           ALF   CMP3
           ORIG EQUIV+0817
           CON  59,5(4-4)
           ORIG TABLE1+0416
           ALF   CMP4
           ORIG EQUIV+0416
           CON  60,5(4-4)
           ORIG TABLE1+0015
           ALF   CMP5
           ORIG EQUIV+0015
           CON  61,5(4-4)
           ORIG TABLE1+0664
           ALF   CMP6
           ORIG EQUIV+0664
           CON  62,5(4-4)
           ORIG TABLE1+0073
           ALF   CMPX
           ORIG EQUIV+0073
           CON  63,5(4-4)
           ORIG TABLE1+0880
           ALF   ORIG
           ORIG EQUIV+0880
           CON  -2,0(4-4)
           ORIG TABLE1+0361
           ALF   EQU
           ORIG EQUIV+0361
           CON  -3,0(4-4)
           ORIG TABLE1+0479
           ALF   CON
           ORIG EQUIV+0479
           CON  -4,0(4-4)
           ORIG TABLE1+0239
           ALF   ALF
           ORIG EQUIV+0239
           CON  -5,0(4-4)
           ORIG TABLE1+0269
           ALF   END
           ORIG EQUIV+0269
           CON  -6,0(4-4)
           ORIG 0B
*
*          SUBROUTINES
*
*      TAPE OUTPUT SUBROUTINES
INSTOUT    STJ  9F
           JBUS *(TAPE)
           LD6  PT1
           CMPX MAXLOC
           JGE  1F
           CMPX EASYLOC
           JE   2F
1H         JMP  SETN
           STX  BASELOC
           STX  TBUF,6(0-4)
           INCX BYTESIZE
           STX  MAXLOC
           DECX BYTESIZE
           ST6  BASEINDEX
           INC6 1
2H         INCX 1
           STX  EASYLOC
           LDA  INST
           STA  TBUF,6
           INC6 1
           LD5  PT2
           JMP  3F
FIXUP      STJ  9F
           JBUS *(TAPE)
           LD5  PT2
           STA  TBUF+1,5
           DEC5 1
           LD6  PT1
3H         CMP6 PT2
SWITCH     JL   4F
           JMP  SETN
           STZ  TBUF+1,5
           STZ  TBUF,6
8H         ENT5 97
           LDAN TBUF+98
           SUB  TBUF,5
           DEC5 1
           J5NN *-2
           STA  TBUF+99
           OUT  TBUF(TAPE)
*          JOV  *+1
           ST5  EASYLOC
           ENT5 97
           ENT6 0
4H         ST6  PT1
           ST5  PT2
9H         JMP  *
SETN       STJ  1F
           LDA  EASYLOC
           JAN  1F
           SUB  BASELOC
           LD1  BASEINDEX
           STA  TBUF,1(5-5)
1H         JMP  *
EASYLOC    CON  -1
PT1        CON  44
PT2        CON  44
*   CARD AND PRINTER SUBROUTINES
PRINTOUT   STJ  1F
           LD1  PBUF
           OUT  0,1(PRINTER)
           LD1  24,1
           ST1  PBUF
           MOVE -1,1(24)
           LDA  LCTR
           CHAR
           STX  -20,1(2-5)
1H         JMP  *
PBUF       CON  PBUF1
READIN     STJ  1F
           LD1  RBUF
           IN   0,1(READER)
           LD2  16,1
           ST2  RBUF
           LD1  PBUF
           INC1 7
           MOVE 0,2(16)
1H         JMP  *
RBUF       CON  RBUF1
ERROR      STJ  1F
           LD2  PBUF
           LDX  0,2(1-3)
           STX  0,2(2-4)
           STA  0,2(1-1)
           LDA  ERRCOUNT
           INCA 1
           STA  ERRCOUNT
1H         JMP  *
EDIT       STJ  9F
           LD1  PBUF
           LDA  UNDFLAG
           JAZ  *+3
           ENTA 47
           STA  0,1(5-5)
           LDX  INST
           JXNN *+4
           ENTA 45
           STA  1,1(1-1)
           LDX  INST(1-5)
           ENT5 5
           ENT6 1,1
           ENN4 5
1H         ENT3 5
           ENTA 0
2H         SLAX 1
           INC4 1
           DEC3 2
           DEC5 1
           LD1  BOUND+5,4
           J1Z  2B
           CHAR
           J3NN 3F
           STX  TEMP
           SRAX 5
           INC3 5
           JMP  EDIT1
           ENT3 0
           LDX  TEMP
3H         JMP  EDIT1
           LDX  INST(1-5)
           SLAX 5,4
           J4NZ 1B
9H         JMP  *
EDIT1      STJ  1F
           J5P  *+4
           INC6 1
           INC5 5
           J5NP *-2
           SLAX 0,3
           ENTA 0
           SLAX 0,5
           ADD  0,6
           STA  0,6
           JXZ  *+2
           STX  1,6
           INC5 -5,3
1H         JMP  *
*     SYMBOL TABLE SUBROUTINE
SERCH      STJ  9F
           STA  TEMPA
           STX  TEMPX
           ADD  TEMPX
           MUL  PHI
           SLAX 5
           MUL  =SIZE=
           STA  TEMP
           LD2  TEMP
           ENT3 1
           LDX  TEMPX
1H         LDA  TABLE1,2
           CMPA TEMPA(1-5)
           JNE  2F
           CMPX TABLE2,2
           JNE  3F
           LDX  EQUIV,2
           JAN  8F
           JMP  9F
2H         JAZ  4F
3H         DEC2 1
           J2NN 1B
           DEC3 1
           ENT2 SIZE-1
           J3Z  1B
           ENTA 22
BADERROR   JMP  ERROR
           JMP  FINISHLOC
4H         LDAN TEMPA
           STA  TABLE1,2
           STX  TABLE2,2
           ENNX 1
8H         LDA  LCTR
           STA  EQUIV,2
           ENT1 1
9H         JMP  *
PHI        CON  618033989//1000000000
*        SCANNING SUBROUTINES
ADDR       LD6  RBUF
           INC6 3
ADDR1      ENN5 4
           STJ  ADDREX1
           STZ  UNDFLAG
           STZ  UNDOP
           STZ  PAREN
INIT       STZ  OP
           STZ  ACCUM
           ENTA 5
           STA  FFIELD
LEXSCAN    ENT1 0
           ENT2 0
           ENT3 5
           ENT4 0
           LDX  0,6
           SLAX 5,5
           ENTA 0
2H         J5N  *+4
           INC6 1
           LDX  0,6
           ENN5 5
           INC5 1
           J4N  8F
           CMPX =30(1-1)=(1-1)
           JL   1F
           CMPX =40(1-1)=(1-1)
           JL   7F
           CMPX =48(1-1)=(1-1)
           JE   3F
           J4P  1F
4H         JANZ 8F
           CMPX =46(1-1)=(1-1)
5H         ENN4 1
           JE   *+3
           STX  NEXTCHAR
           JMP  NOOPERAND
           STX  *+3(1-4)
           LDX  *+2
           JMP  2B
           CON  0
3H         J4P  5B
           ENT4 1
1H         CMPX =0=(1-1)
           JE   4B
           ENT2 1
7H         SLAX 1
           DEC3 1
           J3NZ 2B
           STA  TEMP
           JMP  2B
8H         STX  NEXTCHAR
           ENTX 0
           J3N  1F
           SLAX 0,3
           J4NZ 2F
           J2Z  7F
           STA  TEMP
           LD3  TEMP(1-1)
           DEC3 30
           J3N  5F
           CMPA 6F(2-5)
           JE   4F
           CMPA 9F(2-5)
           JNE  5F
           LDA  BTABLE,3
           JMP  OPERAND
6H         ALF  0F
9H         ALF  0B
2H         JANZ 5F
           LDA  LCTR
           JMP  OPERAND
1H         CMP3 =-5=
           JGE  *+3
           ENTA 13
           JMP  ADDRERROR
           SLC  10,3
           LDA  TEMP
3H         J2Z  7F
5H         JMP  SERCH
           STX  TEMP
           LDA  TEMP
           JMP  OPERAND
7H         SRAX 5,3
           NUM
           JMP  OPERAND
4H         LDA  FTABLE,3
           LDX  LCTR
           STX  FTABLE,3
           ENT1 1
           JMP  OPERAND
NOOPERAND  LD2  OP
           J2NN 1F,2
MISOPERAND ENTA 1
ADDRERROR  JMP  ERROR
           LDA  UNDOP
           STA  INST(0-2)
           JMP  ADDREX
1H         JMP  EXAMINE
           JMP  RPREN
           CMPX 1F(1-1)
           JNE  MISOPERAND
           ENT1 0
           JMP  SETOP
1H         ALF  /
OPERAND    STA  VALUE
           LD2  OP
           J1Z  1F
           STA  UNDOP
           J2Z  LOAD1
FREFERROR  ENTA 24
           JMP  ADDRERROR
1H         JMP  1F,2
           JMP  LPREN
           JMP  COMMA
           JMP  FDIV
           JMP  MUL
           JMP  MINUS
           JMP  ADD
1H         JMP  LOAD
           JMP  EXTOPERAND
           LDA  ACCUM
           ENTX 0
           JMP  1F
MINUS      LD2  PAREN
           J2NZ DASH
           LDA  ACCUM
           SUB  VALUE
           JMP  2F
MUL        MUL  ACCUM
           SLAX 5
           JMP  2F
FDIV       LDA  ACCUM
           SRAX 5
1H         DIV  VALUE
           JMP  2F
ADD        ADD  ACCUM
2H         LD1  UNDFLAG
3H         J1Z  LOAD
           JMP  FREFERROR
LOAD1      ST1  UNDFLAG
           LD1  CONFLAG
           JMP  3B
LPREN      ENTX 1
           STX  PAREN
2H         STA  FFIELD
           JMP  EXAMINE
DASH       LDA  FFIELD
           MUL  =8=
           SLAX 5
           ADD  VALUE
           JMP  2B
RPREN      STZ  PAREN
           LD2  CONFLAG
           J2NZ EXAMINE
           LDA  FFIELD
           STA  INST(4-4)
           JMP  EXAMINE
COMMA      STA  INST(3-3)
           JMP  EXAMINE
LOAD       STA  ACCUM
           JNOV EXAMINE
           ENTA 25
           JMP  ADDRERROR
EXAMINE    LD1  NEXTCHAR(1-1)
           J1Z  BLANK
           DEC1 40
           J1NN *+3
EXTOPERAND ENTA 14
           JMP  ADDRERROR
           DEC1 8
           J1N  SETOP
CHARERROR  ENTA 3
           JMP  ADDRERROR
SETOP      LDA  CHARTABLE,1(0-1)
           STA  OP
           LD1  CHARTABLE,1(2-3)
           JMP  0,1
COMMA1     LD1  CONFLAG
           J1Z  LEXSCAN
           JMP  INSERT
           CON  CHARERROR(2-3)
           CON  -5(0-1),COMMA1(2-3)
           CON  -6(0-1),LEXSCAN(2-3)
           CON  +1(0-1),LEXSCAN(2-3)
           CON  -1(0-1),LEXSCAN(2-3)
           CON  -2(0-1),LEXSCAN(2-3)
           CON  -3(0-1),LEXSCAN(2-3)
           CON  +2(0-1),LEXSCAN(2-3)
CHARTABLE  CON  -4(0-1),LEXSCAN(2-3)
BLANK      LD1  CONFLAG
           J1NZ INSERT
           LDA  ACCUM
           STA  INST(0-2)
ADDREX     LDA  INST
ADDREX1    JMP  *
INSERT     ENTA 0
           LDX  FFIELD
           STX  3F(4-4)
           DIV  =8=
           STA  L
           STX  R
           CMPX =5=
           JG   *+3
           CMPX L
           JGE  *+3
           ENTA 6
           JMP  ADDRERROR
           LD1  R
           ENTA 1
           STA  BOUND,1
           JMP  *+2
           STZ  BOUND,1
           DEC1 1
           CMP1 L
           JGE  *-3
           STA  BOUND,1
           LDA  ACCUM
3H         STA  INST(0)
           LD1  NEXTCHAR(1-1)
           J1Z  ADDREX
           JMP  INIT
*
*      BEGINNING OF ASSEMBLY
*
BEGIN      IOC  0(TAPE)
           IN   RBUF2(READER)
           IOC  0(PRINTER)
           JMP  FIXUP
           ENT1 9
           ENNA 1
           STA  FTABLE,1
           DEC1 1
           J1NN *-2
           ENT1 SIZE-1
           STZ  TABLE2,1
           DEC1 1
           J1NN *-2
*
*         MAIN ASSEMBLY PROCESS
*
FINLINE    JMP  PRINTOUT
READCARD   JMP  READIN
           LDA  0,2(1-1)
           DECA 46
           JAZ  FINLINE
           LDA  2,2(2-5)
           JAZ  FINLINE
PROCESSLOC STZ  FIX1
           LDA  0,2
           JAZ  PROCESSOP
           CMPA 1F(2-5)
           JE   2F
3H         LDX  1,2
           ENT1 0
           JMP  SERCH
           INC2 1
           J1NZ 4F
           ENTA 4
           JMP  ERROR
           JMP  PROCESSOP
1H         ALF  0H
2H         LD2  0,2(1-1)
           DEC2 30
           J2N  3B
           DEC2 10
           J2NN 3B
           LDX  FTABLE+10,2
           ENTA -1
           STA  FTABLE+10,2
4H         ST2  FIX1
           LDA  LCTR
           STA  FIX2
           INCX 1
           STX  FIX3
PROCESSOP  LD2  RBUF
           LDA  2,2(2-5)
           ENTX 0
           JMP  SERCH
           ENT1 BOUND+1
           JXNN 1F
           STX  TEMP
           STX  CONFLAG
           STZ  INST
           LD3  CONFLAG
           JMP  *+7,3
           JMP  END
           JMP  ALF
           JMP  CON
           JMP  EQU
           JMP  ORIG
BADOP      STZ  TABLE1,2
           ENTA 16
           JMP  ERROR
           ENTX 0
1H         STX  INST
           MOVE INSTBOUND(4)
           STZ  CONFLAG
           JMP  ADDR
FINISHINST JMP  EDIT
           LDX  LCTR
           JMP  INSTOUT
           LDA  LCTR
           INCA 1
SETLCTR    STA  LCTR
FINISHLOC  LD2  FIX1
           J2Z  FINLINE
           LDA  FIX2
           J2P  1F
           STA  BTABLE+10,2
           JMP  2F
1H         STA  EQUIV-1,2
           STZ  TABLE1-1,2(0-0)
2H         LDX  FIX3
           JXZ  FINLINE
           SLAX 3
           SRA  2
           SLAX 2
           JMP  FIXUP
           LDA  FIX3
           DECA 1
           CHAR
           LD1  PBUF
           STX  5,1(2-5)
           JMP  FINLINE
*
*       PSEUDO-OPERATIONS
*
EQU        JMP  ADDR
           STA  FIX2
           JMP  FINISHLOC
CON        MOVE CONBOUND(4)
           JMP  ADDR
           JMP  FINISHINST
ALF        MOVE ALFBOUND(4)
           LD2  RBUF
           LDA  3,2
           LDX  4,2
           SLAX 1
           STA  INST
           JMP  FINISHINST
ORIG       JMP  ADDR
           CMPA 1F
           JGE  *+2
           JANN SETLCTR
           ENTA 19
           JMP  BADERROR
1H         CON  1(3-3)
CONBOUND   CON  0
           CON  0
           CON  0
INSTBOUND  CON  0
ALFBOUND   CON  1
           CON  1
           CON  1
           CON  1
*
*     END OF ASSEMBLY
*
END        JMP  ADDR
           STA  STARTLOC
           LDA  1F
           STA  READCARD
           JMP  FINISHLOC
1H         JMP  *+1
           LDA  3F
           STA  READCARD
           ENT1 SIZE
1H         LDA  TABLE1-1,1
           JANN 8F
           LD2  PBUF
           STA  7,2
           LDX  TABLE2-1,1
           STX  8,2
           ST1  FIX1
           LDX  LCTR
           STX  FIX2
           LDX  EQUIV-1,1
           INCX 1
           STX  FIX3
           STZ  INST
           ENT1 BOUND+1
           MOVE CONBOUND(4)
           CMPA =48(1-1)=(1-1)
           JNE  2F
           ENT6 7,2
           JMP  ADDR1
2H         JMP  FINISHINST
3H         JMP  *+1
           LD1  FIX1
8H         DEC1 1
           J1P  1B
           LDA  STARTLOC(4-5)
           SLA  2
           ADD =1(1-1)=
           STZ SWITCH
           JMP FIXUP
           LD2 PBUF
           LDA ERRCOUNT
           CHAR
           STX  0,2
           ENT1 23
           LDA  1F
           STA  1,2
           INC2 1
           DEC1 1
           J1P  *-3
           JMP  PRINTOUT
           OUT  TBUF(TAPE)
           IOC  0(TAPE)
           IOC  0(PRINTER)
           HLT  2
           ENT6 3500
           IN   0,6(TAPE)
           IN   40,6(TAPE)
           JMP  0,6
1H         ALF  .....
           END  BEGIN
